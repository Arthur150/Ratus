plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

Properties loadProperties(String path) {
    def propertiesFile = rootProject.file(path)
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return properties
}

def keystoreProperties = loadProperties("keystore.properties")

android {
    compileSdk 31

    defaultConfig {
        applicationId "fr.univ.lyon1.lpiem.ratus"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0"

        def major = Integer.parseInt(property("version.major"))
        def minor = Integer.parseInt(property("version.minor"))
        def patch = Integer.parseInt(property("version.patch"))
        def buildNumber = Integer.parseInt(property("version.buildNumber"))

        versionCode buildNumber
        versionName "${major}.${minor}.${patch}-b${buildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        production {
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.production
        }
    }

    flavorDimensions "environment"
    productFlavors {
        development {
            dimension "environment"
        }

        production {
            dimension "environment"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

def koin_version = "3.1.6"
def nav_version = "2.4.2"
dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-firestore-ktx:24.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Firebase
    implementation 'com.google.firebase:firebase-firestore:24.1.1'
    implementation 'com.google.firebase:firebase-auth:21.0.3'
    implementation 'com.google.firebase:firebase-auth-ktx:21.0.3'
    implementation 'com.firebaseui:firebase-ui-auth:7.2.0'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    // Koin for Kotlin apps
    implementation "io.insert-koin:koin-android:$koin_version"

    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1'

    // LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"

    // Charts
    implementation("ir.mahozad.android:pie-chart:0.7.0")
}